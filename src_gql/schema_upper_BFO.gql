################
################
# Basic formal ontology and Relation Ontology Core #
################
################
# BFO github wiki: https://github.com/bfo-ontology/BFO/wiki
# RO core github wiki: https://github.com/oborel/obo-relations/wiki/ROCore

  define

  ################
  # ENTITIES #
  ################

  # continuant
  # elucidation: A continuant is an entity that persists, endures, or
  # continues to exist through time while maintaining its identity.
  # BFO 2 Reference: Continuant entities are entities which can be sliced
  # to yield parts only along the spatial dimension, yielding for example
  # the parts of your table which we call its legs, its top, its nails.
  continuant sub entity,
    # plays location-of, # conceptual location, not coordinates
    plays member-of,
    plays exists-at, # at some temporal region. # TODO: rather/also attribute?
    # NB: cf exists-at and occupies-temporal-region. The latter implies fully.
    # https://github.com/BFO-ontology/BFO/issues/116
    plays participates-in, # in a process
    abstract;

  # Entity in BFO maps to Type in Grakn.
  # NB: BFO continuants map to both entities, attributes and roles in Graql.
  ########

  # tinuant
  # definition "b is an independent continuant = Def. b is a continuant
  # which is such that there is no c and no t such that b s-depends_on c at t.
  # An independent-continuant is an instance class
  # see https://github.com/BFO-ontology/BFO/issues/181
  # see https://github.com/BFO-ontology/BFO/issues/182

  independent-continuant sub continuant,
    plays continuant-part-of,
    plays has-continuant-part,
    plays contains-process,
    plays has-two-dimensional-boundary,
    plays occupies-spatial-region, #see BFO 2.0 manual 3.6.4 Location
    abstract;

  ############
  # immaterial entity
  # BFO user's guide: material entities may have immaterial entities as parts –
  # including the entities identified below as sites.
  # E.g. the interior (or ‘lumen’) of your small intestine
  # BFO 2 Reference: Immaterial entities are divided into two subgroups:
  # boundaries and sites, which bound, or are demarcated in relation,
  # to material entities, and which can thus change location, shape and size
  # and as their material hosts move or change shape or size
  # (for example: your nasal passage; the hold of a ship;
  # the boundary of Wales (which moves with the rotation of the Earth)

  immaterial-entity sub independent-continuant,
    abstract;

  ####################
  # BFO user's manual
  # ELUCIDATION: a zero-dimensional continuant fiat boundary is a fiat point
  # whose location is defined in relation to some material entity. [031-001]
  # EXAMPLES: the geographic North Pole; the quadripoint where the boundaries
  # of Colorado, Utah, New Mexico, and Arizona meet, the point of origin of
  # some spatial coordinate system.

  continuant-fiat-boundary sub immaterial-entity,
    plays bearer-of, # it can bear a specifically dependent continuant
    abstract;

  ####################

  zero-dimensional-continuant-fiat-boundary sub continuant-fiat-boundary;
  one-dimensional-continuant-fiat-boundary sub continuant-fiat-boundary;
  two-dimensional-continuant-fiat-boundary sub continuant-fiat-boundary,#,
    plays two-dimensional-boundary-of;

  ################
  # site
  # BFO users' manual: e.g. the interior of an egg, the environment of a cow
  # TODO: add site host relation? https://github.com/BFO-ontology/BFO/issues/36
  site sub immaterial-entity,
    #plays has-generic-dependent,#?
    #plays is-site-of,
    plays located-in,
    plays location-of;


  # spatial region
  # BFO user's manual
  # We recommend that users of BFO region terms specify the coordinate frame
  # in terms of which their spatial and temporal data are represented.
  # When dealing with spatial regions on the surface of the Earth, for example,
  # this will be the coordinate frame of latitude and longitude, potentially
  # supplemented by the dimension of altitude. Lines of latitude and longitude
  # are two-dimensional continuant fiat boundaries which move as the planet
  # rotates and as it moves in orbiting the sun; however, they are by definition
  # at rest relative to the coordinate frame which they determine.

  spatial-region sub immaterial-entity,
   plays has-spatial-occupant,
   plays spatial-projection-of;
  ####################

  zero-dimensional-spatial-region sub spatial-region; # point
  one-dimensional-spatial-region sub spatial-region; # line
  two-dimensional-spatial-region sub spatial-region; # plane
  tree-dimensional-spatial-region sub spatial-region; # volume

  # material entity
  # elucidation "A material entity is an independent continuant that has some
  # portion of matter as proper or improper continuant part.
  # BFO 2 Reference: Material entities (continuants) can preserve
  # their identity even while gaining and losing material parts.
  # EXAMPLES: a photon, a human being..
  # Every material entity is localized and can move in space.
  # Material entities are concretized specifically-dependent-continuants
  # ‘Matter’ is intended to encompass both mass and energy
  # (we will address the ontological treatment of portions of energy in a later
  # version of BFO).

  # NB: The BFO user's guide document emphasizes that Object, Fiat Object Part
  # and Object Aggregate are not intended to be exhaustive of Material Entity.
  # Users are invited to propose new subcategories of Material Entity.]

  material-entity sub independent-continuant,
    plays has-generic-dependent,
    plays derives-from,
    plays derives-into,
    plays located-in,
    plays location-of,
    plays bearer-of-disposition,
    plays bearer-of-function,
    abstract;

  ################
  # objects
  # an object is a maximal causally unified material entity
  # it offers three paradigms of causal unity:
  #   e.g. cells, proteins, molecules
  #   e.g. solid portions of matter
  #   e.g. engineered artifacts

  object sub material-entity,
    abstract;

  # fiat object parts are parts of objects
  # e.g. the wall of the cervical and abdominal parts of the esophagus
  #note that in the ![diagram](http://ontology.buffalo.edu/bfo/BFO2.png)
  # this is just called fiat object but in the user's guide fiat object part
  fiat-object-part sub material-entity,
    abstract;
  # object aggregates
  # e.g. organisms, populations, organizations, institutions
  # On aggregates of spatial regions,
  # see https://github.com/BFO-ontology/BFO/issues/211
  # on parts of members of object aggregates, see
  # https://github.com/BFO-ontology/BFO/issues/161
  object-aggregate sub material-entity,
    plays has-member,
    abstract;

  ########

  ################
  ## BFO dependent-continuants ##
  ################

  # NB: BFO generically-dependent-continuants are abstract patterns such
  # as a genomic sequence or piece of software that can be instantiated.
  # These seem to indeed be different in kind from the computer in which
  # they are instantiated.
  # However, there seems to be no difference in kind between the computer
  # and the glass and materials that concretize it - rather the specific
  # dependency can be conceived as roles in a relation. In other cases,
  # the specifically-dependent-continuant could be a quality, in which
  # case it maps better to Grakn attributes.

  ################
  ### generically-dependent-continuants
  ################

  # Generically dependent continuants are abstract patterns which are
  # instantiated through the generic-dependency relation
  # e.g. Beethoven's ninth symphony

  # discussion on difference between a GDC and SDC and need for
  # GDC - SDC - IDC chain (some see SDC as superfluous)
  # https://github.com/BFO-ontology/BFO/issues/10
  generically-dependent-continuant sub continuant,
    plays generically-depends-on,
    plays is-concretized-as, # relation with a specifically-dependent-continuant
    plays bearer-of-disposition,
    plays bearer-of-function,
    plays continuant-part-of,
    plays has-continuant-part,
    abstract;

  ################
  ### specifically-dependent-continuant
  ################

  # NB: Some BFO one-sided specifically-dependent-continuants (e.g. qualities)
  # map to Grakn attributes, but others (e.g. this specific novel which
  # specifically depends on its concretization on the ink and paper with which
  # it is printed) map to Grakn EntityTypes, others to roles or relations.
  # Only a few cases, such as a realization of a disease, call for this
  # EntityType

  specifically-dependent-continuant sub continuant,
    plays concretizes,
    plays inheres-in,
    plays bearer-of-disposition,
    plays bearer-of-function,
    # plays continuant-part-of,
    # plays has-continuant-part, https://github.com/BFO-ontology/BFO/issues/111
    abstract;

  # quality
  # BFO user's guide: a quality is a specifically dependent continuant that,
  # in contrast to roles and dispositions, does not require any further process
  # in order to be realized. [055-001]
  # e.g. weight, ambient temperature
  # NB: In some cases BFO qualities may map to Grakn attributes, in others
  # to EntityType.
  # NB: discussion on extending has-material-basis to qualities
  quality sub attribute,
    #plays has-material-basis,
    #https://github.com/BFO-ontology/BFO/issues/64
    #https://github.com/BFO-ontology/BFO/issues/13
    abstract,
    datatype string;

  # NB: Seems unnecessary to reify the relation as a quality
  # relational-quality
  # relational-quality sub quality;

  # realizable-entity
  # BFO 2.0 user's manual: To say that b is a realizable entity is to say that
  # b is a specifically dependent continuant that inheres in some
  # independent continuant which is not a spatial region and is of a type
  # instances of which are realized in processes of a correlated type. [058-002]
  # Realizable entities map to either Grakn attributes, roles or entitytypes
  # NB: We let them remain abstract.

  realizable-entity sub specifically-dependent-continuant,
    plays realized-in,#disposition,
    abstract;

  # disposition
  # BFO user's guide
  # e.g.: an atom of element X has the disposition to decay to element Y,
  # the cell wall is disposed to filter chemicals in endocitosis and exocitosis.
  # Each disposition type is associated with one or more characteristic
  # realization process types – types which are instantiated by those processes
  # in which the respective disposition instance is realized.
  # Diseases are dispositions according to OGMS [27]. We are referring to
  # dispositions also when we consider genetic and other risk factors for
  # specific diseases. The latter are predispositions to disease – in other
  # words they are dispositions to acquire certain further dispositions.
  # The realization of such a predisposition consists in processes which change
  # the physical makeup of its bearer in such a way that parts of this bearer
  # then serve as the material basis for a disease.
  #
  # disposition, function or role? https://github.com/BFO-ontology/BFO/issues/3
  disposition sub realizable-entity,#,
    #plays has-material-basis,
    #https://github.com/BFO-ontology/BFO/issues/64
    #https://github.com/BFO-ontology/BFO/issues/13
    plays disposition-of;

  ########################
  # BFO user's guide: A function is a disposition that exists in virtue of the
  # bearer’s physical make-up and this physical make-up is something the bearer
  # possesses because it came into being, either through evolution
  # (in the case of natural biological entities) or through intentional design
  # (in the case of artifacts), in order to realize processes of a certain sort.
  # [064-001]

  function sub disposition,#,
    plays function-of;

  # role sub realizable-entity;
  # not needed in Grakn

  ################
  ## BFO OCCURRENTS ##
  ################
  # BFO 2.0 user's manual: Occurrents are processes, or the boundaries
  # of processes, or temporal or spatial temporal regions.
  # BFO 2 Reference: (occurrent class) every occurrent that is not a temporal or
  # spatiotemporal region is s-dependent on some independent continuant
  # that is not a spatial region.
  # We map (spatio)temporal regions to an EntityType entity-occurrent class and
  # s-dependent occurrents to relations between EntityTypes
  # NB: according to BFO occurrents are change and cannot have changeable
  # attributes such as velocity
  ################
  # entity-occurrent
  ################

  # see issue re: domain/range: https://github.com/BFO-ontology/BFO/issues/15
  entity-occurrent sub entity,
    # plays precedes, # probably not needed; implied by attributes
    # plays preceded-by,
    abstract;

  # temporal-region
  # elucidation "A temporal region is an occurrent entity that is part of time
  # as defined relative to some reference frame.

  temporal-region sub entity-occurrent,
    plays at-which-exists,
    plays temporal-projection-of,
    plays has-temporal-occupant,
    abstract;

  zero-dimensional-temporal-region sub temporal-region;
  one-dimensional-temporal-region sub temporal-region;

  # spatiotemporal region
  spatiotemporal-region sub entity-occurrent,
    plays projects-onto-spatial-region,
    plays projects-onto-temporal-region,
    # plays occurrent-part-of,
    # plays has-occurrent-part, # probably not needed, rather occupant?
    plays has-spatiotemporal-occupant,
    abstract;

  # process
  # definition "p is a process = Def. p is an occurrent that
  # has temporal proper parts and for some time t, p s-depends_on
  # some material entity at t.
  # proposed generic processes: https://github.com/BFO-ontology/BFO/issues/178
  # see https://github.com/BFO-ontology/BFO/issues/110
  process sub entity-occurrent, #20191005 changed from relation-occurrent
    #relates participant, # a material entity
    plays has-participant,
    plays realizes,
    #relates realized-disposition, # a disposition/role which the participant is bearer-of
    plays occurs-in; # Paraphrase of definition: a relation between a process
    # and an independent continuant, in which the process takes place entirely
    # within the independent continuant

  # realization
  # Paraphrase of elucidation: a relation between a process and
  # a realizable entity, where there is some material entity that is bearer of
  # the realizable entity and participates in the process, and
  # the realizable entity comes to be realized in the course of the process
  # example: this disease course realizes this disease
  # example: this shattering realizes this fragility
  realization sub relation-occurrent,
    relates realized-in,
    relates realizes;

  # participation
  # a relation between a continuant and a process, in which the continuant is
  # somehow involved in the process
  # example: this blood coagulation has participant this blood clot
  participation sub relation-occurrent,
    relates participates-in,
    relates has-participant;
    #has tstart, #TODO introduce such attributes
    #has tstop;
  # BFO history
  # user's manual: Histories are thus very special kinds of processes, since
  # not only is it the case that, for any material entity a, there is exactly
  # one process which is the history of a, but also is it the case that for
  # every history there is just one material entity of which it is the history.
  # NB: future BFO may require material objects and sites to have histories
  # https://github.com/BFO-ontology/BFO/issues/199
  # NB: Add relation rel-during and class history-segment #176
  # https://github.com/BFO-ontology/BFO/issues/176
  # history sub process,#,
  #  relates history-participant as participant,
  #  relates realized-history as realized-disposition;

  # TODO: inheritence forces
  # process-profile and history to inheret roles from process which may not
  # be appropriate, at least in their current form. Review.


  # process profile
  # definition "b is a process_profile =Def. there is some process c such that
  # b process_profile_of c (axiom label in BFO2 Reference: [093-002])"
  # BFO user's guide: the changing through time of a specific set of
  # qualities of a process, e.g.
  # process: heart beat
  # process quality: heart beat rate (at time t0)
  # process profile: heart beat rate profile between t0 and t1
  # Definition under review, see https://github.com/BFO-ontology/BFO/issues/213

  # process-profile sub process,
  #   relates process-profile-participant as participant,
  #   relates realized-profile as realized-disposition;

  # TODO: inheritence forces process-profile and history to inheret roles from
  # process which may not be appropriate, at least in their current form. Review.
  #  relates participant,  # a material entity
  #  relates realized;
    #TODO add attributes:

  # NB: BFO user's manual distinguishes between quality process profiles
  # according to their derivative w.r.t time, e.g. position vs speed. Probably
  # not necessary.

  # process-boundary
  # NB: omit for now until needed - seems fairly complex
  # definition "p is a process boundary =Def. p is a temporal part of a process
  # & p has no proper temporal parts.
  # See also discussion on whether process boundaries need to be s-dependent
  # on continuant (not directly relevant since we have processes as Grakn rel)
  # https://github.com/BFO-ontology/BFO/issues/26
  #process-boundary sub relation-occurrent,
  #  relates ..
  #  plays exists-at, # some temporal-region
  #  plays occupies-spatiotemporal-region; # analogous to exists-at

  ################
  # relation-occurrent
  ################

  relation-occurrent sub relation,
    # TODO relates
    plays precedes,
    plays preceded-by,
    plays exists-at, # TODO: rather/also an attribute?
    plays occupies-temporal-region, # TODO: rather/also an attribute?
    plays occupies-spatiotemporal-region, # TODO: rather/also an attribute?
    plays occurrent-part-of,
    plays has-occurrent-part,
    abstract;

  ################
  ## BFO / RO RELATIONS ##
  ################


  # existence
  # elucidation "b exists_at t means: b is an entity which exists at some
  # temporal region t.
  # TODO: can we not generalise existence to spatiotemporal existence?
  existence sub relation-occurrent,
    relates exists-at,
    relates at-which-exists;
    #abstract;

  # elucidation "p occupies_temporal_region t. This is a primitive relation
  # between an occurrent p and the temporal region t upon which
  # the spatiotemporal region p occupies_spatiotemporal_region projects.
  temporal-region-occupation sub existence,
    relates occupies-temporal-region as exists-at,
    relates has-temporal-occupant as at-which-exists;

  temporal-projection sub existence,
    relates projects-onto-temporal-region as exists-at, # onto temporal region
    relates temporal-projection-of as at-which-exists; # of

  # derives
  # a relation between two distinct material entities, the new entity and the
  # old entity, in which the new entity begins to exist when the old entity
  # ceases to exist, and the new entity inherits the significant portion of
  # the matter of the old entity. E.g. this cell derives from this parent cell.
  derives sub relation-occurrent,
    relates derives-from,
    relates derives-into;

  # two-dimensional-boundary-relation
  # NB: omit until needed
  two-dimensional-boundary-relation sub relation-occurrent,
    relates two-dimensional-boundary-of,
    relates has-two-dimensional-boundary;

  # spatial region

  # region-occupation sub relation,
  #   relates occupies-region,
  #   relates has-occupant,
  #   abstract;

  # spation-region occupation
  # elucidation "b occupies_spatial_region r at t means that r is
  # a spatial region in which independent continuant b is exactly located
  spatial-region-occupation sub relation-occurrent,#region-occupation,
    relates occupies-spatial-region,# as occupies-region,
    relates has-spatial-occupant;# as has-occupant;

  # elucidation "p occupies_spatiotemporal_region s. 
  # This is a primitive relation between an occurrent p and
  # the spatiotemporal region s which is its spatiotemporal extent.
  spatiotemporal-region-occupation sub relation-occurrent,#region-occupation,
    relates occupies-spatiotemporal-region,# as occupies-region, # an occurrent
    relates has-spatiotemporal-occupant;#as has-occupant;

  # projection onto spatial region
  # TODO: Github issue: 'projects on to temporal region' subpropertyof
  # 'occupies spatial region' #183
  # https://github.com/BFO-ontology/BFO/issues/183
  spatial-region-projection sub relation-occurrent,#region-occupation,
    relates projects-onto-spatial-region,#as occupies-region,
    relates spatial-projection-of;# as has-occupant;

  # partition
  partition sub relation-occurrent,
    relates part-of,
    relates has-part,
    abstract;

  continuant-partition sub relation-occurrent,
    relates continuant-part-of as part-of,
    relates has-continuant-part as has-part;

  # NB: We do not (yet) include BFO proper continuant 3
  occurrent-partition sub partition,
    relates occurrent-part-of as part-of,
    relates has-occurrent-part as has-part;
    # e.g. your heart beating from 4pm to 5pm today is a temporal  of
    # the process of your heart beating; the 4th year of your life is a
    # temporal  of your life.

  # spatiotemporal-partition
  # NB: Not in BFO 2.1?
  # occurrent-partition sub partition,
  #   relates occurrent-part-of as part-of,
  #   relates has-occurrent-part as has-part;

  # membership
  # IAO_0000115 "is member of is a mereological relation between a
  # item and a collection."
  # example: IAO_0000112 "An organism that is a member of a population of
  # organisms"

  membership sub relation-occurrent,#partition,
    relates has-member,# as has-part, #TODO: added this role, seems necessary.
    relates member-of;# as part-of;

  # BFO 2.1 doesn't seem to have occurrent membership
  # occurrent-membership sub membership,
  #   relates has-occurrent-member as has-member,
  #   relates occurrent-member-of as member-of;
  #
  # continuant-membership sub membership,
  #   relates has-continuant-member as has-member,
  #   relates continuant-member-of as member-of;

  # precedence
  # RO relation
  # NB BFO 2.0 user's manual: Preceded_by, defined in RO, is not defined in the
  # BFO2 Reference, except by citation to a paper. That paper does not
  # provide axioms on the relation.
  # The RO page definition is suboptimal as the quantification and
  # type of t (instant, interval) isn't stated
  precedence sub relation-occurrent,
    relates precedes,
    relates preceded-by;

  # occurs in
  # A relation between a process and an independent continuant in which the
  # process takes place entirely within the independent continuant
  occurrence sub relation-occurrent,
    relates contains-process,
    relates occurs-in; # material entity or immaterial entity

  # BFO located_in links independent-continuants that are not spatial regions.
  # e.g. John is in London, Carlo is in his mother’s womb.
  location sub relation-occurrent,
     relates located-in,
     relates location-of; # rather than has_location
     # https://github.com/BFO-ontology/BFO/issues/184

  ################
  ## BFO dependency ##
  ################

  # concretization
  # The instantiated entity is merely a copy produced according to the
  # instructions in the instantiating entity
  # elucidation: b concretizes c at t means: b is a specifically dependent
  # continuant & c is a generically dependent continuant & for some
  # independent continuant that is not a spatial region d, b s-depends_on d at t
  # & c g-depends on d at t & if c migrates from bearer d to another bearer e
  # than a copy of b will be created in e.
  # Example of usage: You may concretize a recipe that you find in a cookbook
  # by turning it into a plan which exists as a realizable dependent continuant
  # in your head.
  concretization sub relation-occurrent,
    relates is-concretized-as, # generically dependent continuant,
    # e.g. the chess-board pattern, a DNA sequence
    relates concretizes; # specifically dependent continuant,

  generic-dependency sub relation-occurrent,
    relates generically-depends-on, # generic dependent continuant, e.g. pdf
    relates has-generic-dependent; # independent continuant, e.g. computer ?

  # s-dependencies are such that the concretized entity ceases to exist
  # if the concretizing entity does; the latter points to a substrate
  # for some instance, such as the ink and paper for a copy of a s-dependent
  # novel. The novel in turn  instantiates some g-dependent pattern.
  # definition "b bearer_of c at t =Def. c s-depends_on b at t & b is an
  # independent continuant that is not a spatial region.
  # bearing
  # Discussion on bearer-of as inverse of inheres-in (seems not a problem):
  # https://github.com/BFO-ontology/BFO/issues/144.

  # TODO: add material-basis-of relation? See BFO OWL and
  # https://github.com/BFO-ontology/BFO/issues/113
  # https://github.com/BFO-ontology/BFO/issues/13

  bearing-of sub relation-occurrent,
    relates bearer-of, # independent continuant that is not a spatial region
    relates inheres-in, # specifically dependent continuant
    abstract;

  bearing-of-disposition sub bearing-of,
    relates bearer-of-disposition,
    relates disposition-of;

  bearing-of-function sub bearing-of,
    relates bearer-of-function,
    relates function-of;

  # BFO/RO also has relations for quality and role but we map these to
  # Grakn attributes and relation roles

  # correlated with
  # RO
  # 'curator note' "Groups both positive and negative correlation"
  # NB: no directionality
  correlation-relation sub relation-occurrent, #TODO: subclass e.g. associated-with
    relates correlate; #

  # causally related to
  # RO
  # definition: "This relation groups causal relations between material entities
  # and causal relations between processes"
  # editor note: To define causal relations in an activity-flow type network,
  # we make use of 3 primitives:
 # * Temporal: how do the intervals of the two occurrents relate?
 # * Is the causal relation regulatory? i.e. can the system make use of it?
 # * Is the influence positive or negative
 # NB: We subclass to correlation-relation, since causation invariably implies
 # correlation (at least in the vague sense of non-independence)
  causal-relation sub correlation-relation,
    relates causes as correlate,
    relates caused as correlate, #TODO: are multiple role subclasses possible?
    # TODO: add temporal attributes
    # TODO: add sign attribute
    # TODO: add magnitude attribute
    abstract;
  # \t
  # causal relation between material entities
  # definition source: https://github.com/oborel/obo-relations/wiki/causal-relations
  # editors note: The intent is that the process branch of
  # the causal property hierarchy is primary (causal relations hold
  # between occurrents/processes), and that the material branch
  # is defined in terms of the process branch
  # domain: material-entity
  # ranges: material-entity
  # NB: unclear whether subclassing required as long as class and roles make
  #  Hence we omit most subclasses
  # semantics of each case clear
  causal-relation-between-material-entities sub causal-relation,
    relates causes,
    relates caused;#,
    #abstract;

  # causal relation between material entity and a process
  # definition: A relationship that holds between a material entity and
  # a process in which causality is involved, with either the material entity
  # or some part of the material entity exerting some influence over the process,
  # or the process influencing some aspect of the material entity.
  # definition source: https://github.com/oborel/obo-relations/wiki/causal-relations
  causal-relation-between-material-entity-and-a-process sub causal-relation,
    relates causes,
    relates caused;#,
    #abstract;

  # causal relation between processses
  # RO relation
  # definition: p is causally related to q if and only if p or any part of p
  # and q or any part of q are linked by a chain of events where each event pair
  # is one of direct activation or direct inhibition. p may be upstream,
  # downstream, part of or a container of q.
  # definition source: http://purl.obolibrary.org/obo/ro/docs/causal-relations
  causal-relation-between-processes sub causal-relation,
    relates causes,
    relates caused;#,
    #abstract;

  # TODO: consider subclassing
  # functional-relation
  # RO
  # definition "A grouping relationship for any relationship directly
  # involving a function, or that holds because of a function of one of
  # the related entities."
  # curator note: This is a grouping relation that collects relations used
  # for the purpose of connecting structure and function
  # NB: cf  the bearing-of-function relation (as we label it in Grakn), which
  # relatesa function and the bearer in which the function inheres.

  # functional-relation sub relation-occurrent,
  #   relates functionally-related,
  #   abstract;
  # \t
  # omitted [...]

  # enables
  # alternative term: catalyzes, executes
  # curator note: This relation differs from the parent relation 'capable of'
  # in that the parent is weaker and only expresses a capability that may not
  # be actually realized, whereas this relation is always realized.
  # enabling sub functional-relation,
  #   relates enabled-by as functionally-related,
  #   relates enables as functionally-related;
  # omitted [...]

  # input
  input sub participation,
    relates has-input as has-participant,
    relates input-of as participates-in;
  output sub participation,
    relates has-output as has-participant,
    relates output-of as participates-in;

  # in similarity relationship with
  # definition: Relation between biological objects that resemble or are related
  # to each other sufficiently to warrant a comparison.
  # seeAlso: http://www.ontobee.org/ontology/HOM?iri=http://purl.obolibrary.org/obo/HOM_0000000
  # synonym: sameness, resemblance
  similarity sub relation-occurrent,
    relates similar-to;

  functional-equivalence sub similarity,
    relates functially-equivalent-to as similar-to;

  # [...]

  # definition "Relation between a research artifact and an entity it is used
  # to study, in virtue of its replicating or approximating features of the
  # studied entity has."
  # 'editor note' "To Do: decide on scope of this relation - inclusive of
  # computational models in domain, or only physical models?  Restricted to
  # linking biological systems and phenomena?  Inclusive of only diseases in
  # range, or broader?"
  # NB: We use in the broader sense that includes a logical or computational
  # model
  model-relation sub relation-occurrent,
    relates has-model,
    relates is-model-of;

  # related via evidence of inference to
  evidence-relation sub relation-occurrent,
    relates has-evidence, # material entity or process
    relates is-evidence-for; # an information content entity

  # mereotopolotically related to
  mereotopological-relation sub relation-occurrent,
    relates mereotopologically-related-to,
    abstract;
  # [...]
  # attached to
  # a is attached to b if and only if a and b are discrete objects or
  # object parts, and there are physical connections between a and b such that
  # a force pulling a will move b, or a force pulling b will move a
  attachment sub mereotopological-relation,
    relates attached-to as mereotopologically-related-to;

  # colocalizes with
  # definition: a colocalizes_with b if and only if  a is transiently or
  # peripherally associated with b[GO].
  # curator note: In the context of the Gene Ontology, colocalizes_with may
  # be used for annotating to cellular component terms[GO]
  # range, domain : material entity
  colocalization sub mereotopological-relation,
    relates colocalizes-with as mereotopologically-related-to;

  # continuous-with
  # definition: X continuous_with Y if and only if X and Y share a fiat boundary.
  continuity sub mereotopological-relation,
    relates continuous-with as mereotopologically-related-to;

  # in register with
  # definition: A relationship that holds between two images, A and B, where:
  # A depicts X;
  # B depicts Y;
  # X and Y are both of type T'
  # C is a 2 layer image consiting of layers A and B;
  # A and B are aligned in C according to a shared co-ordinate framework so
  # that common features of X and Y are co-incident with each other.
  # Note: A and B may be 2D or 3D.
  # Examples include: the relationship between two channels collected
  # simultaneously from a confocal microscope; the relationship between
  # an image dpeicting X and a painted annotation layer that delineates regions
  # of X; the relationship between the tracing of a neuron on an EM stack and
  # the co-ordinate space of the stack; the relationship between two separately
  # collected images that have been brought into register via some image
  # registration software.
  register-relation sub mereotopological-relation,
    relates in-register-with as mereotopologically-related-to;
  # sequentially related to
  # curator note: The domain and range of this relation include entities
  # such as: information-bearing macromolecules such as DNA, or regions
  # of these molecules; abstract information entities encoded as
  # a linear sequence including text, abstract DNA sequences; Sequence features, entities that have a sequence or sequences. Note that these entities are not necessarily contiguous - for example, the mereological sum of exons on a genome of a particular gene.

  # overlap
  # has curation status: pending final vetting
  overlap sub mereotopological-relation,
    relates overlaps-with as mereotopologically-related-to;

  # spatially disjoint from
  # curator note: Note that it would be possible to use the relation to label
  # the relationship between a near infinite number of structures.
  # The intent is that this is used for parsiomoniously for disambiguation
  # purposes - for example, between siblings in a jointly exhaustive
  # pairwise disjointness hierarchy
  # has curation status: pending final vetting
  spatial-disjointedness sub mereotopological-relation,
    relates spatially-disjoint-from as mereotopologically-related-to;

  # produced by / produces
  # definition: a produced_by b iff some process that occurs_in b has_output a.
  # domain / range: material entity (cf. output )
  production sub relation-occurrent,
    relates produces,
    relates produced-by;

  # temporally related to
