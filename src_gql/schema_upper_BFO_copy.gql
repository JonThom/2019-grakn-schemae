################
################
# Basic formal ontology #
################
################

# Classes, patterns and instantiations
# A **data base** encodes information about **instances** of
# abstract concepts (cat:Garfield) and patterns (CAGCTGAT:cell 1).
# A **knowledge base** incodes information about the attributs and relations
# between these abstract concepts and patterns **themselves**.
# It is probably desirable to be able to model both instances and categeries
# in a single schema.
# Grakn makes a division into universals (types) in the schema, which specifies
# their **possible** relations and attributes, but not the **actual** relations,
# which are set out at the data level between 'entities'.
# However, we might want to both specify actual relations and attributes
# between universals (e.g. between helium atom and nuclear fusion) **and** between
# instances (the design and authors of a particular experiment.) within a single
# schema. This means that the Grakn 'entity' data layer must accommodate both
# 'patterns' (e.g. the gene Fox2 with the sequence CAGGTACT.., an instance of
# the EntityType gene) and concretizations of patterns (this specific molecule).
# I.e. we need two kinds of instantiation:
# 1. abstract concept -> pattern (has pattern..)
# 2. pattern -> instance (has location, time..)
# However in Grakn the schema-data level interface seems to force a choice
# between one or the other.
#
# Solution:
# Model classes (homo sapiens) as generically dependent continuants and
# concretizations (Peter Nielsen) as independent continuants

  define

  ################
  # ENTITIES #
  ################

  # continuant
  # elucidation: A continuant is an entity that persists, endures, or
  # continues to exist through time while maintaining its identity.
  # BFO 2 Reference: Continuant entities are entities which can be sliced
  # to yield parts only along the spatial dimension, yielding for example
  # the parts of your table which we call its legs, its top, its nails.
  continuant sub entity,
    # plays participant,
    # plays location-of, # conceptual location, not coordinates
    # plays located-in, #
    plays continuant-part-of,
    plays has-continuant-part,
    # plays member-of,
    # plays has-disposition,
    # plays has-function,
    plays exists-at, # exists at some temporal region.
    abstract;

  # Entity in BFO maps to Type in Grakn.
  # NB: BFO continuants map to both entities, attributes and roles in Graql.
  ########

  # independent-continuant
  # definition "b is an independent continuant = Def. b is a continuant
  # which is such that there is no c and no t such that b s-depends_on c at t.
  # An independent-continuant is an instance class
  independent-continuant sub continuant,
    # plays bearer-of,
    # # see https://github.com/BFO-ontology/BFO/issues/182
    # plays contains-process,
    # plays has-two-dimensional-boundary,
    plays occupies-spatial-region, #see BFO 2.0 manual 3.6.4 Location
    abstract;

  ############
  # immaterial entity
  # BFO user's guide: material entities may have immaterial entities as parts –
  # including the entities identified below as sites.
  # E.g. the interior (or ‘lumen’) of your small intestine
  # BFO 2 Reference: Immaterial entities are divided into two subgroups:
  # boundaries and sites, which bound, or are demarcated in relation,
  # to material entities, and which can thus change location, shape and size
  # and as their material hosts move or change shape or size
  # (for example: your nasal passage; the hold of a ship;
  # the boundary of Wales (which moves with the rotation of the Earth)

  immaterial-entity sub independent-continuant,
    abstract;

  ####################
  # BFO user's manual
  # ELUCIDATION: a zero-dimensional continuant fiat boundary is a fiat point
  # whose location is defined in relation to some material entity. [031-001]
  # EXAMPLES: the geographic North Pole; the quadripoint where the boundaries
  # of Colorado, Utah, New Mexico, and Arizona meet, the point of origin of
  # some spatial coordinate system.

  continuant-fiat-boundary sub immaterial-entity,
      abstract;

  ####################

  zero-dimensional-continuant-fiat-boundary sub continuant-fiat-boundary;
  one-dimensional-continuant-fiat-boundary sub continuant-fiat-boundary;
  two-dimensional-continuant-fiat-boundary sub continuant-fiat-boundary;#,
  #  plays two-dimensional-boundary-of;

  ################
  # site
  # BFO users' manual: e.g. the interior of an egg, the environment of a cow
  site sub immaterial-entity;

  # spatial region
  # BFO user's manual
  # We recommend that users of BFO region terms specify the coordinate frame
  # in terms of which their spatial and temporal data are represented.
  # When dealing with spatial regions on the surface of the Earth, for example,
  # this will be the coordinate frame of latitude and longitude, potentially
  # supplemented by the dimension of altitude. Lines of latitude and longitude
  # are two-dimensional continuant fiat boundaries which move as the planet
  # rotates and as it moves in orbiting the sun; however, they are by definition
  # at rest relative to the coordinate frame which they determine.

  spatial-region sub immaterial-entity,
   plays has-spatial-occupant,
   plays spatial-projection-of;
  ####################

  zero-dimensional-spatial-region sub spatial-region; # point
  one-dimensional-spatial-region sub spatial-region; # line
  two-dimensional-spatial-region sub spatial-region; # plane
  tree-dimensional-spatial-region sub spatial-region; # volume

  # material entity
  # elucidation "A material entity is an independent continuant that has some
  # portion of matter as proper or improper continuant part.
  # BFO 2 Reference: Material entities (continuants) can preserve
  # their identity even while gaining and losing material parts.
  # EXAMPLES: a photon, a human being..
  # Every material entity is localized and can move in space.
  # Material entities are concretized specifically-dependent-continuants
  # ‘Matter’ is intended to encompass both mass and energy
  # (we will address the ontological treatment of portions of energy in a later
  # version of BFO).

  # NB: The BFO user's guide document emphasizes that Object, Fiat Object Part
  # and Object Aggregate are not intended to be exhaustive of Material Entity.
  # Users are invited to propose new subcategories of Material Entity.]

  material-entity sub independent-continuant,
    plays derives-from,
    plays derives-into,
    abstract;

  ################
  # objects
  # an object is a maximal causally unified material entity
  # it offers three paradigms of causal unity:
  #   e.g. cells, proteins, molecules
  #   e.g. solid portions of matter
  #   e.g. engineered artifacts

  object sub material-entity,
    abstract;

  # fiat object parts are parts of objects
  # e.g. the wall of the cervical and abdominal parts of the esophagus
  #note that in the ![diagram](http://ontology.buffalo.edu/bfo/BFO2.png)
  # this is just called fiat object but in the user's guide fiat object part
  fiat-object-part sub material-entity,
    abstract;
  # object aggregates
  # e.g. organisms, populations, organizations, institutions
  object-aggregate sub material-entity,
  #  plays has-member,
    abstract;

  ########

  ################
  ## BFO dependent-continuants ##
  ################

  # NB: BFO generically-dependent-continuants are abstract patterns such
  # as a genomic sequence or piece of software that can be instantiated.
  # These seem to indeed be different in kind from the computer in which
  # they are instantiated.
  # However, there seems to be no difference in kind between the computer
  # and the glass and materials that concretize it - rather the specific
  # dependency can be conceived as roles in a relation. In other cases,
  # the specifically-dependent-continuant could be a quality, in which
  # case it maps better to Grakn attributes.

  ################
  ### generically-dependent-continuants
  ################

  # Generically dependent continuants are abstract patterns which are
  # instantiated through the generic-dependency relation
  # e.g. Beethoven's ninth symphony

  generically-dependent-continuant sub continuant,
  #  plays is-concretized-as, # relation with a specifically-dependent-continuant
    abstract;

  ################
  ### specifically-dependent-continuant
  ################

  # NB: Some BFO one-sided specifically-dependent-continuants (e.g. qualities)
  # map to Grakn attributes, but others (e.g. this specific novel which
  # specifically depends on its concretization on the ink and paper with which
  # it is printed) map to Grakn EntityTypes, others to roles or relations.
  # Only a few cases, such as a realization of a disease, call for this
  # EntityType

  specifically-dependent-continuant sub continuant,
  #  plays concretizes,
    abstract;

  # quality
  # BFO user's guide: a quality is a specifically dependent continuant that,
  # in contrast to roles and dispositions, does not require any further process
  # in order to be realized. [055-001]
  # e.g. weight, ambient temperature
  # NB: In some cases BFO qualities may map to Grakn attributes, in others
  # to EntityType.

  quality sub attribute,
    abstract,
    datatype string;

  # NB: Seems unnecessary to reify the relation as a quality
  # relational-quality
  # relational-quality sub quality;

  # realizable-entity
  # BFO 2.0 user's manual: To say that b is a realizable entity is to say that
  # b is a specifically dependent continuant that inheres in some
  # independent continuant which is not a spatial region and is of a type
  # instances of which are realized in processes of a correlated type. [058-002]
  # EXAMPLES: the role of being a doctor, the role of this boundary to delineate
  # where Utah and Colorado meet..
  # Realizable entities map to either Grakn attributes or roles

  realizable-entity sub specifically-dependent-continuant,
  #  plays realized-in,
    abstract;

  # disposition
  # BFO user's guide
  # e.g.: an atom of element X has the disposition to decay to element Y,
  # the cell wall is disposed to filter chemicals in endocitosis and exocitosis.
  # Each disposition type is associated with one or more characteristic
  # realization process types – types which are instantiated by those processes
  # in which the respective disposition instance is realized.
  # Diseases are dispositions according to OGMS [27]. We are referring to
  # dispositions also when we consider genetic and other risk factors for
  # specific diseases. The latter are predispositions to disease – in other
  # words they are dispositions to acquire certain further dispositions.
  # The realization of such a predisposition consists in processes which change
  # the physical makeup of its bearer in such a way that parts of this bearer
  # then serve as the material basis for a disease.

  disposition sub realizable-entity;#,
  #  plays disposition-of;

  ########################
  # BFO user's guide: A function is a disposition that exists in virtue of the
  # bearer’s physical make-up and this physical make-up is something the bearer
  # possesses because it came into being, either through evolution
  # (in the case of natural biological entities) or through intentional design
  # (in the case of artifacts), in order to realize processes of a certain sort.
  # [064-001]

  function sub disposition;#,
  #  plays function-of;

  # role sub realizable-entity;
  # not needed in Grakn

  ################
  ## BFO OCCURRENTS ##
  ################

  # NB: We map BFO occurrents to Grakn entities as well as relations
  # BFO 2 Reference: (occurrent class) every occurrent that is not a temporal or
  # spatiotemporal region is s-dependent on some independent continuant
  # that is not a spatial region.
  # We map (spatio)temporal regions to an EntityType region-occurrent class and
  # s-dependent occurrents to relations between EntityTypes

  ################
  # region-occurrent
  ################

  region-occurrent sub entity,
    plays precedes,
    plays preceded-by,
    abstract;

  # temporal-region
  # elucidation "A temporal region is an occurrent entity that is part of time
  # as defined relative to some reference frame.

  temporal-region sub region-occurrent,
    plays at-which-exists,
    plays temporal-projection-of,
    plays temporal-part-of,
    plays has-temporal-part,
    plays has-temporal-occupant,
    abstract;

  zero-dimensional-temporal-region sub temporal-region;
  one-dimensional-temporal-region sub temporal-region;

  # spatiotemporal region
  spatiotemporal-region sub region-occurrent,
    plays projects-onto-spatial-region,
    plays projects-onto-temporal-region,
    plays spatiotemporal-part-of,
    plays has-spatiotemporal-part,
    plays has-spatiotemporal-occupant,
    abstract;

  ################
  # relation-occurrent
  ################

  relation-occurrent sub relation,
    # TODO relates
    plays precedes,
    plays preceded-by,
    plays exists-at,
    plays occupies-temporal-region,
    plays occupies-spatiotemporal-region,
    abstract;

  # process
  # BFO user's guide: Those natural  units which do exist in the realm of
  # occurrents are typically either parasitic on the existence of natural units
  # on the continuant side, or they are fiat in nature
  # Processes are defined by fiat and can be arbitrarily summed and divided.
  # In particular, we can identify sub-processes – temporal parts – which are
  # fiat segments occupying constituent temporal intervals of the
  # temporal interval occupied by the process as a whole. Occurrents are
  # processes, or the boundaries of processes, or temporal or spatial temporal
  # regions.
  # NB: we map BFO processes, including actions, to Grakn relations with a
  # cardinality of at least 1 i.e. a heart beating process is a relation
  # involving the heart. See BFO user's guide 3.11.2-3

  #NB: we map the realizes/realized-in relations to process relation roles
  # IAO_0000600 "[copied from inverse property 'realizes '] to say that b
  # realizes c at t is to assert that there is some material entity d & b is a
  # process which has participant d at t & c is a disposition or role of which
  # d is bearer_of at t& the type instantiated by b is correlated with the type
  # instantiated by c. (axiom label in BFO2 Reference: [059-003])"@en

  # process sub relation,
  #   relates participant, # a material entity # TODO move up to relation-occurrent? z
  #   relates realized-in, # a disposition/role which the participant is bearer-of
  #   plays occurs-in; # Paraphrase of definition: a relation between a process
    # and an independent continuant, in which the process takes place entirely
    # within the independent continuant
    # NB: according to BFO occurrents do not change and cannot have changeable
    # attributes such as velocity

  # BFO history
  # user's manual: Histories are thus very special kinds of processes, since
  # not only is it the case that, for any material entity a, there is exactly
  # one process which is the history of a, but also is it the case that for
  # every history there is just one material entity of which it is the history.
  # history sub process;
    #relates has-history, # NB: we convert history to a relation and the BFO 2.1
    # relations to roles
    #relates history-of;
    # TODO: subclass process roles?
    # TODO: add attributes. Time?

  # process profile
  # BFO user's guide: the changing through time of a specific set of
  # qualities of a process, e.g.
  # process: heart beat
  # process quality: heart beat rate (at time t0)
  # process profile: heart beat rate profile between t0 and t1

  # process-profile sub process;
    #relates has-process-profile,
    #relates process-profile-of; # NB as with histoyr, we convert process and
    # process profile to a relation and the corresponding BFO 2.1 relations
    # to roles. Test if This works.
    #relates has-profile as participant, # a material entity
    #relates process-profile-of as realized-in;
    #TODO add attributes:

  # NB: BFO user's manual distinguishes between quality process profiles
  # according to their derivative w.r.t time, e.g. position vs speed. Probably
  # not necessary.

  # process-boundary sub occurrent;
  #   # TODO

  ################
  ## BFO / RO RELATIONS ##
  ################

  # BFO user's guide 2.2
  # Note that relations of none of these sorts are first-class entities
  # (to see why not, see the discussion of the Bradley regress in [20]).
  # However, there are first-class entities, such as relational qualities
  # and relational processes (see below), which are relational in the sense
  # that they link multiple relata. First-class entities are entities which
  # have counterparts both at the level of instances (John’s act of
  # kissing Mary yesterday) and at the level of universals (kiss, act, person).

  # existence
  # elucidation "b exists_at t means: b is an entity which exists at some
  # temporal region t.
  existence sub relation-occurrent,
    relates exists-at,
    relates at-which-exists;
    #abstract;

  # elucidation "p occupies_temporal_region t. This is a primitive relation
  # between an occurrent p and the temporal region t upon which
  # the spatiotemporal region p occupies_spatiotemporal_region projects.
  temporal-region-occupation sub existence,
    relates occupies-temporal-region as exists-at,
    relates has-temporal-occupant as at-which-exists;

  temporal-projection sub existence,
    relates projects-onto-temporal-region as exists-at, # onto temporal region
    relates temporal-projection-of as at-which-exists; # of

  # derives
  # a relation between two distinct material entities, the new entity and the
  # old entity, in which the new entity begins to exist when the old entity
  # ceases to exist, and the new entity inherits the significant portion of
  # the matter of the old entity. E.g. this cell derives from this parent cell.
  derives sub relation-occurrent,
    relates derives-from,
    relates derives-into;

  # two-dimensional-boundary-relation
  # two-dimensional-boundary-relation sub relation,
  #   relates two-dimensional-boundary-of,
  #   relates has-two-dimensional-boundary;

  # spatial region

  # region-occupation sub relation,
  #   relates occupies-region,
  #   relates has-occupant,
  #   abstract;

  spatial-region-occupation sub relation-occurrent,#region-occupation,
    relates occupies-spatial-region,# as occupies-region,
    relates has-spatial-occupant;# as has-occupant;

  # elucidation "p occupies_spatiotemporal_region s. 
  # This is a primitive relation between an occurrent p and
  # the spatiotemporal region s which is its spatiotemporal extent.
  spatiotemporal-region-occupation sub relation-occurrent,#region-occupation,
    relates occupies-spatiotemporal-region,# as occupies-region, # an occurrent
    relates has-spatiotemporal-occupant;#as has-occupant;

  # projection onto spatial region
  spatial-region-projection sub relation-occurrent,#region-occupation,
    relates projects-onto-spatial-region,#as occupies-region,
    relates spatial-projection-of;# as has-occupant;

  # partition
  partition sub relation-occurrent,
    relates part-of,
    relates has-part,
    abstract;

  continuant-partition sub relation-occurrent,
    relates continuant-part-of as part-of,
    relates has-continuant-part as has-part;

  # NB: We do not (yet) include BFO proper continuant 3

  temporal-partition sub partition,
    relates temporal-part-of as part-of,
    relates has-temporal-part as has-part;
    # e.g. your heart beating from 4pm to 5pm today is a temporal  of
    # the process of your heart beating; the 4th year of your life is a
    # temporal  of your life.

  spatiotemporal-partition sub partition,
    relates spatiotemporal-part-of as part-of,
    relates has-spatiotemporal-part as has-part;
  # membership
  # membership sub partition;
  #   relates has-member as has-part,
  #   relates member-of as part-of;

  # precedence
  # NB BFO 2.0 user's manual: Preceded_by, defined in RO, is not defined in the
  # BFO2 Reference, except by citation to a paper. That paper does not
  # provide axioms on the relation.
  # The RO page definition is suboptimal as the quantification and
  # type of t (instant, interval) isn't stated.

  precedence sub relation-occurrent,
    relates precedes,
    relates preceded-by;
  # occurs in
  # A relation between a process and an independent continuant in which the
  # process takes place entirely within the independent continuant
  # occurrence sub occurrent;
  #   relates contains-process,
  #   relates occurs-in; # material entity or immaterial entity

  # BFO located_in links independent-continuants that aren not spatial regions.
  # e.g. John is in London, Carlo is in his mother’s womb.
  # location sub occurrent;
  #    relates located-in,
  #    relates location-of;

  ################
  ## BFO dependency ##
  ################

  # concretization
  # The instantiated entity is merely a copy produced according to the
  # instructions in the instantiating entity
  # elucidation: b concretizes c at t means: b is a specifically dependent
  # continuant & c is a generically dependent continuant & for some
  # independent continuant that is not a spatial region d, b s-depends_on d at t
  # & c g-depends on d at t & if c migrates from bearer d to another bearer e
  # than a copy of b will be created in e.
  # Example of usage: You may concretize a recipe that you find in a cookbook
  # by turning it into a plan which exists as a realizable dependent continuant
  # in your head.
  # concretization sub occurrent;
  #   relates is-concretized-as, # generically dependent continuant,
  #   # e.g. the chess-board pattern, a DNA sequence
  #   relates concretizes; # specifically dependent continuant,

  # s-dependencies are such that the concretized entity ceases to exist
  # if the concretizing entity does; the latter points to a substrate
  # for some instance, such as the ink and paper for a copy of a s-dependent
  # novel. The novel in turn  instantiates some g-dependent pattern.

  # bearing sub occurrent;
  #   relates bearer-of,
  #   relates inheres-in,
  #   abstract;

  # having-disposition sub bearing;
  #   relates has-disposition,
  #   relates disposition-of;

  # having-function sub bearing;
  #   relates has-function,
  #   relates function-of;

  # BFO/RO also has relations for quality and role but we map these to
  # Grakn attributes and relation roles
